 r←test_find sink;ctl;NL;hdn;res;_Spawn
 r←''
 NL←⎕UCS ⎕SE.SALTUtils.LINDEL
 #.S.GoTo'file:///',path,'Tests/test_find.html'

⍝ 1. id
 ctl←#.S.Find'usernameInput'
 'John Doe'Assert ctl.GetAttribute⊂'value'

 ctl←#.S.Find'submitButton'
 'Submit Form'Assert ctl.Text


⍝ 2. Name[s]
 ctl←'Name'#.S.Find'userEmail'
 'input'Assert ctl.TagName
 'john@doe.com'Assert ctl.GetAttribute⊂'value'

 ctl←'Names'#.S.Find'gender'
 2 Assert ctl.Count
 'male'Assert ctl[0].GetAttribute⊂'value'
 'female'Assert ctl[1].GetAttribute⊂'value'


⍝ 3. Class Name[s]
 ctl←'ClassName'#.S.Find'intro-paragraph'
 'This is an introductory paragraph.'Assert ctl.Text

 ctl←'ClassNames'#.S.Find'highlighted-text'
 2 Assert ctl.Count
 'This text is important.'Assert ctl[0].Text
 'Another important text.'Assert ctl[1].Text


⍝ 4. Tag Name
 ctl←'TagName'#.S.Find'a'
 'Clickable Link'Assert ctl.Text
 ctl←'TagNames'#.S.Find'li'
 6 Assert ctl.Count  ⍝

⍝ 5. [Partial] Link Text
 ctl←'LinkText'#.S.Find'Visit Google'
 'https://www.google.com/'Assert ctl.GetAttribute⊂'href'

 ctl←'PartialLinkText'#.S.Find'Bing'
 'https://www.bing.com/'Assert ctl.GetAttribute⊂'href'

⍝ 6. Css Selector
 ctl←'CssSelector'#.S.Find'#mainContent'
 ('Content inside main area.',NL,'Contact us here.')Assert ctl.Text
 ctl←'CssSelector'#.S.Find'.content-area'
 ('Content inside main area.',NL,'Contact us here.')Assert ctl.Text
 ctl←'CssSelector'#.S.Find'[data-qa="footer-info"]'
 'Contact us here.'Assert ctl.Text
 ctl←'CssSelector'#.S.Find'div.element-box > p'
 'This is an introductory paragraph.'Assert ctl.Text
 ctl←'CssSelector'#.S.Find'li.menu-item.active'
 'Item Two'Assert ctl.Text


⍝ 7. XPath
 ctl←'XPath'#.S.Find'//div[@id="xpathContainer"]/p[1]'
 'Paragraph 1'Assert ctl.Text
 ctl←'XPath'#.S.Find'//span[contains(text(), "text")]'
 'Some text'Assert ctl.Text
 ctl←'XPath'#.S.Find'//*[@id="xpathContainer"]//span'
 'Some text'Assert ctl.Text
 ctl←'XPath'#.S.Find'//button[@type="submit"]'
 'Submit'Assert ctl.Text

⍝ 8. Dealing with hidden elements
⍝ this is from AB (more robust than my original attempt that used ⎕DL)
_Spawn←{
     nil←()
     _←nil.⎕DF'⍰'
     ⍺←⊢
     result←⍺{
         (
             Ready:0
             Value:nil
         )
     }⍵
     _←result.⎕FX(
         'r←Sync'
         ':trap 6'
         ':if Ready ⋄ r←Value ⋄ :else ⋄ r←⎕TSYNC Tid ⋄ :endif'
         ':else ⋄ r←Value'
         ':endtrap'
     )
     _←⍺ ⍺⍺{
         ⍺←⊢
         ⍵⍵.Value←⍺ ⍺⍺ ⍵
         ⍵⍵.Ready←1
     }result&⍵
     result
}
 ctl←#.S.Find'hiddenElement'
 0 Assert ctl.Displayed    ⍝ initially it's hidden
 0 Assert #.S.Click ctl   ⍝ can't interact with it
 ⍝ hdn←'hiddenElement'#.S.WaitFor&'This element is hidden initially.'   ⍝ use opportunity to also test "WaitFor"
 hdn←'hiddenElement'#.S.WaitFor _Spawn'This element is hidden initially.'   ⍝ use opportunity to also test "WaitFor"
 #.S.Click'toggleHiddenButton'⍝ make element visible
 ⍝ 0 Assert≢⎕TSYNC hdn       ⍝ has WaitFor found the element after it was clicked?
 :Repeat
 ⎕dl .01
 :Until hdn.Ready
 0 Assert≢hdn.Value        ⍝ has WaitFor found the element after it was clicked?
 1 Assert ctl.Displayed    ⍝ is it?
 1 Assert #.S.Click ctl    ⍝ and clickable...?

 #.S.Click'toggleHiddenButton'⍝ hide it again
 hdn←{r←#.S.(IsVisible Retry ⍵) ⋄ sink←⎕DL 2 ⋄ r}&'hiddenElement'   ⍝ ⎕DL so that ⎕TSYNC will find the thread...
 #.S.Click'toggleHiddenButton'⍝ hide it again
 res←⎕TSYNC hdn
 1 Assert≢res                 ⍝ is it visible?

 ⍝ 9. Find ListItems
 ctl←'menuList'#.S.FindListItems'Item Two'
 326 Assert ⎕DR ctl               ⍝ have we found an object?
 1 Assert≢ctl                    ⍝ exactly one item?
 (,⊂'Item Two')Assert ctl.Text   ⍝ does the text match?
 'i2'Assert(⊃ctl).GetAttribute⊂'id'

 ⍝ 10. Test PageSource
 ctl←#.S.BROWSER.PageSource
 1 Assert∨/'<meta charset="UTF-8">'⍷ctl   ⍝ not exactly a "Find" test, but still useful
